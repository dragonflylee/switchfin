# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: averne <averne381@gmail.com>
# Contributor: Cpasjuste <cpasjuste@gmail.com>

pkgbasename=libmpv
pkgname=switch-${pkgbasename}
pkgver=0.36.0
pkgrel=5
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=(
  "${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" 
  "hos-audio.patch" "ass-fonts.patch" "mp-image.patch" "lavfi-channel.patch"
)
sha256sums=(
  '29abc44f8ebee013bb2f9fe14d80b30db19b534c679056e4851ceadf5a5e8bf6'
  'SKIP' 'SKIP' 'SKIP' 'SKIP'
)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=('switch-ffmpeg' 'switch-mesa')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  rm -f audio/out/ao_hos.c osdep/switch/sys/mman.h
  patch -Np1 -i "$srcdir/hos-audio.patch"
  patch -Np1 -i "$srcdir/ass-fonts.patch"
  patch -Np1 -i "$srcdir/mp-image.patch"
  patch -Np1 -i "$srcdir/lavfi-channel.patch"
}

build() {
  cd mpv-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./bootstrap.py
  export CFLAGS="$CFLAGS -D_POSIX_VERSION=200809L -DNDEBUG -I`pwd`/osdep/switch"
  LIBDIR=${PORTLIBS_PREFIX}/lib TARGET=aarch64-none-elf ./waf configure \
    --prefix="${PORTLIBS_PREFIX}" --disable-libmpv-shared --disable-debug-build \
    --enable-libmpv-static --disable-cplayer --disable-sdl2 --disable-lua --enable-hos-audio \
    --disable-iconv --disable-jpeg --disable-libavdevice --disable-libarchive 
  sed -i 's/#define HAVE_POSIX 1/#define HAVE_POSIX 0/' build/config.h
  ./waf build
}

package() {
  cd mpv-$pkgver

  ./waf install --destdir=$pkgdir
}