# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: averne <averne381@gmail.com>
# Contributor: Cpasjuste <cpasjuste@gmail.com>

pkgbasename=libmpv
pkgname=switch-${pkgbasename}-deko3d
pkgver=0.36.0
pkgrel=6
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=(
  "${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" 
  "hos-audio.patch" "deko3d.patch" "lavfi-channel.patch"
)
sha256sums=(
  '29abc44f8ebee013bb2f9fe14d80b30db19b534c679056e4851ceadf5a5e8bf6'
  'SKIP' 'SKIP' 'SKIP'
)
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
depends=('switch-ffmpeg' 'deko3d' 'libuam' 'switch-liblua51')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/hos-audio.patch"
  patch -Np1 -i "$srcdir/deko3d.patch"
  patch -Np1 -i "$srcdir/lavfi-channel.patch"
}

build() {
  cd mpv-$pkgver

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt \
    -Dlibmpv=true -Dcplayer=false -Dtests=false \
    -Dgl=disabled -Degl=disabled -Dsdl2=disabled -Diconv=disabled  \
    -Dlibarchive=disabled -Dlua=enabled -Djpeg=disabled \
    -Dhos-audio=enabled -Ddeko3d=enabled build

  meson compile -C build
}

package() {
  cd mpv-$pkgver

  DESTDIR="$pkgdir" meson install -C build
}