# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: averne <averne381@gmail.com>
# Contributor: Cpasjuste <cpasjuste@gmail.com>

pkgbasename=libmpv
pkgname=switch-${pkgbasename}-deko3d
pkgver=0.39.0
pkgrel=4
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=(
  "${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" 
  "hos-audio.patch" "deko3d.patch"
)
sha256sums=(
  '2ca92437affb62c2b559b4419ea4785c70d023590500e8a52e95ea3ab4554683'
  'SKIP' 'SKIP'
)
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
depends=('switch-ffmpeg' 'switch-libplacebo' 'deko3d' 'libuam' 'switch-liblua51')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/hos-audio.patch"
  patch -Np1 -i "$srcdir/deko3d.patch"
}

build() {
  cd mpv-$pkgver

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt \
    -Dlibmpv=true -Dcplayer=false -Dtests=false -Dlua=enabled -Dhos-audio=enabled \
    -Dplain-gl=enabled -Degl=disabled -Dsdl2=disabled \
    -Dlibarchive=disabled -Djpeg=disabled -Diconv=disabled -Ddeko3d=enabled build

  meson compile -C build
}

package() {
  cd mpv-$pkgver

  DESTDIR="$pkgdir" meson install -C build
}