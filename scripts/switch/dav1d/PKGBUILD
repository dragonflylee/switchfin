# Contributor:  averne <averne381@gmail.com>

pkgbasename=dav1d
pkgname=switch-$pkgbasename
pkgver=1.4.0
pkgrel=1
pkgdesc="dav1d is an AV1 cross-platform decoder, open-source, and focused on speed and correctness."
arch=("any")
url="https://code.videolan.org/videolan/${pkgbasename}"
license=("BSD")
options=(!strip libtool staticlibs)
source=(
  "https://downloads.videolan.org/pub/videolan/dav1d/${pkgver}/dav1d-${pkgver}.tar.xz"
  "dav1d.patch"
)
sha256sums=(
  'ac8f8ff581906c8ef6479370cf6c1465f282fd2e7cfc2c6366c772f84ad9d30e'
  '424e132380ea9afb0e13fcdf5d2893a7895094fff4ee297cdb30ead422256542'
)
makedepends=('dkp-toolchain-vars' 'dkp-meson-scripts')
groups=('switch-portlibs')

prepare() {
  cd ${pkgbasename}-${pkgver}

  patch -Np1 -i "$srcdir/dav1d.patch"
}

build() {
  cd ${pkgbasename}-${pkgver}

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt build -Dc_std=gnu11 -Db_ndebug=true -Denable_asm=true \
    -Denable_tools=false -Denable_examples=false -Denable_tests=false -Denable_docs=false
  meson compile -C build
}

package() {
  cd ${pkgbasename}-${pkgver}

  DESTDIR="$pkgdir" meson install -C build
}
