# Contributor:  averne <averne381@gmail.com>

pkgbasename=dav1d
pkgname=switch-$pkgbasename
pkgver=1.5.0
pkgrel=1
pkgdesc="dav1d is an AV1 cross-platform decoder, open-source, and focused on speed and correctness."
arch=("any")
url="https://code.videolan.org/videolan/${pkgbasename}"
license=("BSD")
options=(!strip libtool staticlibs)
source=(
  "https://downloads.videolan.org/pub/videolan/dav1d/${pkgver}/dav1d-${pkgver}.tar.xz"
  "dav1d.patch"
)
sha256sums=(
  '14bd6f5157808ed9aedcafbe50df689d304fd4810ac20be6eec1ab037436afd6'
  'b9b48db3f8b1c070f6fb1581f0cea666f34a3dd3f53cd0dd018980c985b36515'
)
makedepends=('dkp-toolchain-vars' 'dkp-meson-scripts')
groups=('switch-portlibs')

prepare() {
  cd ${pkgbasename}-${pkgver}

  patch -Np1 -i "$srcdir/dav1d.patch"
}

build() {
  cd ${pkgbasename}-${pkgver}

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt build -Dc_std=gnu11 -Db_ndebug=true -Denable_asm=true \
    -Denable_tools=false -Denable_examples=false -Denable_tests=false -Denable_docs=false
  meson compile -C build
}

package() {
  cd ${pkgbasename}-${pkgver}

  DESTDIR="$pkgdir" meson install -C build
}
