# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=freetype
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.13.3
pkgrel=1
pkgdesc="TrueType font rendering library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'clangarm64')
msys2_references=(
  'archlinux: freetype2'
)
url="https://www.freetype.org/"
license=('spdx:GPL-2.0-or-later OR FTL')
depends=(
  "${MINGW_PACKAGE_PREFIX}-bzip2"
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
options=('staticlibs' '!strip')
source=("https://downloads.sourceforge.net/project/freetype/freetype2/${pkgver}/freetype-${pkgver}.tar.xz")
sha256sums=('0550350666d427c74daeb85d5ac7bb353acba5f76956395995311a9c6f063289')

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --buildtype=release \
      --default-library=static \
      -Dharfbuzz=disabled \
      -Dtests=disabled \
      -Dbrotli=disabled \
      "build-${MSYSTEM}" \
      "${srcdir}/${_realname}-${pkgver}"

  meson compile -C "build-${MSYSTEM}"
}

package () {
  meson install -C "build-${MSYSTEM}" --destdir "${pkgdir}"
}